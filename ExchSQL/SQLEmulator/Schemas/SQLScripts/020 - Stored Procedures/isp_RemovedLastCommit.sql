--/////////////////////////////////////////////////////////////////////////////
--// Filename		: isp_RemovedLastCommit.sql
--// Author		: Nilesh Desai
--// Date		: 28 July 2008
--// Copyright Notice	: (c) 2015 Advanced Business Software & Solutions Ltd. All rights reserved.
--// Description	: SQL Script to add isp_RemovedLastCommit stored procedure.
--//                  This SP will delete records from HISTORY and DETAILS table
--// Usage : EXEC @ReturnValue = [!ActiveSchema!].[isp_RemovedLastCommit] 
--//         EXEC [!ActiveSchema!].[isp_RemovedLastCommit] 
--//
--/////////////////////////////////////////////////////////////////////////////
--// Version History:
--//	1	: File Creation
--//
--/////////////////////////////////////////////////////////////////////////////

IF EXISTS ( SELECT	TOP 1 1
		    FROM    dbo.sysobjects 
		    WHERE	id = OBJECT_ID(N'[!ActiveSchema!].[isp_RemovedLastCommit]') 
			AND		OBJECTPROPERTY(id,N'IsProcedure') = 1
		  )

DROP PROCEDURE [!ActiveSchema!].[isp_RemovedLastCommit]

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [!ActiveSchema!].[isp_RemovedLastCommit]	
AS
BEGIN
	-- Constants
	DECLARE		@c_PLNHCode			INT
			  ,	@c_BankNHCode       INT
			  ,	@c_CtrlNHCode       INT
			  , @c_NomHedCode		INT
			  , @c_StkBillCode      INT
			  , @c_CommitOrdRunNo	INT	
			  , @c_CommitKey		VARCHAR(6)

	-- Assigning Constants values
	SELECT		@c_PLNHCode         = 65
			  ,	@c_BankNHCode       = 66        
			  ,	@c_CtrlNHCode       = 67        
			  , @c_NomHedCode		= 72 
			  , @c_StkBillCode      = 77 
			  , @c_CommitOrdRunNo	= -53										-- Run Time Lines generated by commitment module
			  , @c_CommitKey		= 'CMT' + REPLICATE(CHAR(2), 2) + CHAR(33)	-- CommitKey for deleting records from HISTORY

	-- Local Variables
	DECLARE		@ReturnCode			INT

	-- Assigning default values
	SELECT		@ReturnCode			= 0

	BEGIN TRY

		-- Delete records from HISTORY
		DELETE
		FROM	[!ActiveSchema!].[HISTORY]
		WHERE	hiExClass IN (  @c_PLNHCode
							  , @c_BankNHCode
							  , @c_CtrlNHCode
							  , @c_NomHedCode
							  , @c_StkBillCode
							 )
		AND		SUBSTRING(hiCodeComputed, 1, LEN(@c_CommitKey)) = @c_CommitKey

		-- Delete from records from DETAILS for RunNo = -53
		DELETE
		FROM	[!ActiveSchema!].[DETAILS]
		WHERE	tlRunNo = @c_CommitOrdRunNo
			
	END TRY
	BEGIN CATCH
       	-- Execute error logging routine
	    EXEC common.isp_RaiseError   @iv_IRISExchequerErrorMessage = 'Procedure [!ActiveSchema!].[isp_RemovedLastCommit]' -- Include optional message...?
		-- SP failed - error raised
		SET @ReturnCode = -1	
    END CATCH

	SET NOCOUNT OFF
	RETURN @ReturnCode
END
GO